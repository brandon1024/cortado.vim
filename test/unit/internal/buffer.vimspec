Describe cortado#internal#buffer
	Before
		let buffers = cortado#internal#buffer#new()
	End

	Describe #lnum_matching_patt
		Before
			%bwipeout!
		End

		It should find first occurrence of a pattern in the buffer
			edit test/input/StaticImports.java

			let lnum = buffers.lnum_matching_patt('%', 3, '^import static')
			Assert Equals(lnum, 3)
		End

		It should return lnum only if the pattern matches ahead of the given lnum
			edit test/input/StaticImports.java

			let lnum = buffers.lnum_matching_patt('%', 4, '^import static')
			Assert Equals(lnum, 0)
		End

		It should return zero if no line matching pattern found
			edit test/input/StaticImports.java

			let lnum = buffers.lnum_matching_patt('%', 4, 'what is this?')
			Assert Equals(lnum, 0)
		End

		It should return zero if the line number is negative or zero
			edit test/input/StaticImports.java

			let lnum = buffers.lnum_matching_patt('%', -1, 'public')
			Assert Equals(lnum, 0)
			let lnum = buffers.lnum_matching_patt('%', 0, 'public')
			Assert Equals(lnum, 0)
		End

		It should return zero if the line number is greater than the number of lines in the buffer
			edit test/input/StaticImports.java

			let lnum = buffers.lnum_matching_patt('%', 100, 'public')
			Assert Equals(lnum, 0)
		End

		It should find lines matching in specific buffer
			edit test/input/StaticImports.java
			vsp test/input/ImportStaticMethod.java

			let lnum = buffers.lnum_matching_patt('test/input/StaticImports.java', 3, '^import static')
			Assert Equals(lnum, 3)

			let lnum = buffers.lnum_matching_patt('test/input/ImportStaticMethod.java', 100, 'public')
			Assert Equals(lnum, 0)
		End
	End

	Describe #lines_matching_patt
		Before
			%bwipeout!
		End

		It should return all lines matching given pattern
			edit test/input/StaticImports.java

			let lines = buffers.lines_matching_patt('%', 1, 'public')
			Assert Equals(len(lines), 2)

			Assert Equals(lines[0], 'public class StaticImports {')
			Assert Equals(lines[1], '    public StaticImports(ImportedClass c, Interface i) {}')
		End

		It should return empty list if no lines match pattern
			edit test/input/StaticImports.java

			let lines = buffers.lines_matching_patt('%', 1, 'publics')
			Assert Equals(len(lines), 0)
		End

		It should return all lines matching given pattern in specific buffer
			edit test/input/StaticImports.java
			vsp test/input/ImportStaticMethod.java

			let lines = buffers.lines_matching_patt('test/input/StaticImports.java', 1, 'public')
			Assert Equals(len(lines), 2)
			let lines = buffers.lines_matching_patt('test/input/ImportStaticMethod.java', 1, 'ca.example.vim')
			Assert Equals(len(lines), 3)
		End
	End

	Describe #filter_lines_matching_patt
		Before
			%bwipeout!
		End

		It should correctly remove and return lines matching the given pattern
			edit test/input/StaticImports.java

			let initial_linecount = line('$')

			let lines = buffers.filter_lines_matching_patt('%', 1, 'import')
			Assert Equals(len(lines), 8)

			let lines = buffers.lines_matching_patt('%', 1, 'import')
			Assert Equals(len(lines), 0)

			Assert Equals(initial_linecount - line('$'), 8)
		End

		It should correctly remove and return lines matching the given pattern for specific buffer
			edit test/input/StaticImports.java
			vsp test/input/ImportStaticMethod.java

			let lines = buffers.filter_lines_matching_patt('test/input/StaticImports.java', 1, 'import')
			Assert Equals(len(lines), 8)

			let lines = buffers.lines_matching_patt('test/input/StaticImports.java', 1, 'import')
			Assert Equals(len(lines), 0)
			let lines = buffers.lines_matching_patt('test/input/ImportStaticMethod.java', 1, 'import')
			Assert Equals(len(lines), 2)
		End
	End

	Describe #trunc_to_patt
		Before
			%bwipeout!
		End

		It should truncate all lines up to stopping condition pattern and return line number of stopping condition pattern occurrence
			edit test/input/StaticImports.java

			let lnum = buffers.trunc_to_patt(1, '.*', 'public')
			Assert Equals(lnum, 1)
		End

		It should return 0 if stopping condition pattern never found
			edit test/input/StaticImports.java

			let lnum = buffers.trunc_to_patt(1, '.*', 'no exists')
			Assert Equals(lnum, 0)
		End
	End

	Describe #write
		Before
			%bwipeout!
		End

		It should insert lines at given lnum in correct order
			edit test/input/StaticImports.java
			
			call buffers.write(3, ['my-test-1', 'my-test-2'])

			let num1 = buffers.lnum_matching_patt('%', 1, 'my-test-1')
			Assert Equals(4, num1)

			let num2 = buffers.lnum_matching_patt('%', 1, 'my-test-2')
			Assert Equals(5, num2)
		End

		It should flatten the list of lines
			edit test/input/StaticImports.java
			
			call buffers.write(3, [['my-test-1'], ['my-test-2']])

			let num1 = buffers.lnum_matching_patt('%', 1, 'my-test-1')
			Assert Equals(4, num1)

			let num2 = buffers.lnum_matching_patt('%', 1, 'my-test-2')
			Assert Equals(5, num2)
		End
	End
End
